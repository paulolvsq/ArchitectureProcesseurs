
;  ### -------------------------------------------------------------- ###
;  # file	: qsort_m.u						#
;  # date	: Sep  8 2009						#
;  # descr.	: functional test for Mips 32				#
;  ### -------------------------------------------------------------- ###

	;  ### ------------------------------------------------------ ###
	;  #   data section						#
	;  ### ------------------------------------------------------ ###

			.data

data_table :		.word	 12,   2,  56,   7, 542
			.word	  7,  87,   2,   4,  98

head_str   :		.ascii	"table : "
first_nbr  :		.space	 16

separ_str  :		.ascii	", "
other_nbrs :		.space	 16

data_table_size		.equ	 10

	;  ### ------------------------------------------------------ ###
	;  #   text section						#
	;  #     - noreorder						#
	;  ### ------------------------------------------------------ ###

			.text
			.set	noreorder

	;  ### ------------------------------------------------------ ###
	;  #   make the label _main visible for other files		#
	;  ### ------------------------------------------------------ ###

			.globl	_main

;  ### -------------------------------------------------------------- ###
;  # function	: _main							#
;  #									#
;  # description: sort and print a table of integers			#
;  #									#
;  # parameters	: int *tab, unsigned int size				#
;  # return	: none							#
;  #									#
;  # called func: _print_integer_table,	_qsort,	_exit			#
;  # syscall	: none							#
;  ### -------------------------------------------------------------- ###

_main :
			addiu	r29, r29, -2*4

			la	r16, data_table
			la	r17, data_table_size

			move	r4 , r16
			move	r5 , r17
			jal	_print_integer_table

			move	r4 , r16
			move	r5 , r17
			jal	_qsort

			move	r4 , r16
			move	r5 , r17
			jal	_print_integer_table

	;  ### ------------------------------------------------------ ###
	;  #   exit							#
	;  ### ------------------------------------------------------ ###

			addi	r4 , r0 , 0		; exit value
			jal	_exit
			nop

			addiu	r29, r29, -2*4
			jr	r31

;  ### -------------------------------------------------------------- ###
;  # function	: _qsort						#
;  #									#
;  # description: quick sort						#
;  #									#
;  # parameters	: int *tab, unsigned int size				#
;  # return	: none							#
;  #									#
;  # called func: none							#
;  # syscall	: none							#
;  ### -------------------------------------------------------------- ###

_qsort :

	;  ### ------------------------------------------------------ ###
	;  #   save							#
	;  ### ------------------------------------------------------ ###

			addiu	r29, r29,  -3*4
			sw	r16,        0*4 (r29)
			sw	r17,        1*4 (r29)
			sw	r31,        2*4 (r29)

			move	r16, r4
			move	r17, r5
			beqz	r5 , _qsort_end

	;  ### ------------------------------------------------------ ###
	;  #   partition						#
	;  ### ------------------------------------------------------ ###

			jal	_qsort_pivot_partition
			nop

	;  ### ------------------------------------------------------ ###
	;  #   recursive call						#
	;  ### ------------------------------------------------------ ###

			move	r4 , r16
			move	r5 , r2

			sll	r6 , r2 , 2
			addu	r16, r16, r6
			addiu	r16, r16,  4
			subu	r17, r17, r2
			addiu	r17, r17, -1

			jal	_qsort
			nop

			move	r4 , r16
			move	r5 , r17
			jal	_qsort

	;  ### ------------------------------------------------------ ###
	;  #   return							#
	;  ### ------------------------------------------------------ ###

_qsort_end :
			lw	r31,        2*4 (r29)
			lw	r17,        1*4 (r29)
			lw	r16,        0*4 (r29)

			addiu	r29, r29,   3*4
			jr	r31

;  ### -------------------------------------------------------------- ###
;  # function	: _qsort_pivot_partition				#
;  #									#
;  # description: quick sort partition					#
;  #									#
;  # parameters	: int *tab, unsigned int size				#
;  # return	: unsigned int						#
;  #									#
;  # called func: none							#
;  # syscall	: none							#
;  ### -------------------------------------------------------------- ###

_qsort_pivot_partition :

	;  ### ------------------------------------------------------ ###
	;  ### ------------------------------------------------------ ###

			move	r6 , r4
			sll	r5 , r5 ,  2
			addu	r5 , r4 , r5

			addiu	r4 , r4 ,  4
			lw	r8 ,      -4 (r4 )

			move	r9 , r4
			beq	r4 , r5 , _qsort_pivot_partition_end_loop

_qsort_pivot_partition_loop :
			lw	r10, 0 (r4 )
			lw	r11, 0 (r9 )
			sw	r10, 0 (r9 )
			sw	r11, 0 (r4 )
			slt	r11, r10, r8
			addiu	r4 , r4 , 4
			sll	r11, r11, 2
			addu	r9 , r9 , r11
			bne	r4 , r5 , _qsort_pivot_partition_loop

			lw	r10, -4 (r9)
			sw	r8 , -4 (r9)
			sw	r10,  0 (r6)

	;  ### ------------------------------------------------------ ###
	;  #   return							#
	;  ### ------------------------------------------------------ ###

_qsort_pivot_partition_end_loop :

			subu	r2 , r9 , r6
			addiu	r2 , r2 , -4
			srl	r2 , r2 , 2
			jr	r31

;  ### -------------------------------------------------------------- ###
;  # function	: _print_integer_table					#
;  #									#
;  # description: print a table of integers				#
;  #									#
;  # parameters	: int *tab, unsigned int size				#
;  # return	: none							#
;  #									#
;  # called func: _itoa_d, _fputs					#
;  # syscall	: none							#
;  ### -------------------------------------------------------------- ###

_print_integer_table :

	;  ### ------------------------------------------------------ ###
	;  #   save							#
	;  ### ------------------------------------------------------ ###

			addiu	r29, r29,  -5*4
			sw	r16,        0*4 (r29)
			sw	r17,        1*4 (r29)
			sw	r18,        2*4 (r29)
			sw	r19,        3*4 (r29)
			sw	r31,        4*4 (r29)

	;  ### ------------------------------------------------------ ###
	;  #   initializations						#
	;  ### ------------------------------------------------------ ###

			move	r16, r4
			beq	r5 , r0 , _print_integer_table_end

			sll	r6 , r5 , 2
			addu	r17, r16, r6

	;  ### ------------------------------------------------------ ###
	;  #   print the integer table					#
	;  ### ------------------------------------------------------ ###

			la	r5, first_nbr
			lw	r4 , 0 (r16)
			jal	_itoa_d

			addiu	r16, r16,  4

			la	r4 , head_str
			move	r5 , r0
			jal	_fputs

			la	r18 , separ_str
			la	r19 , other_nbrs
			beq	r16, r17, _print_integer_table_print_eol

_print_integer_table_loop :
			lw	r4 , 0 (r16)
			move	r5 , r19
			jal	_itoa_d

			addiu	r16, r16,  4

			move	r4 , r18
			move	r5 , r0
			jal	_fputs

			bne	r16, r17, _print_integer_table_loop
			nop

_print_integer_table_print_eol :
			ori	r4 , r0 , '\n'
			jal	_putchar
			nop

	;  ### ------------------------------------------------------ ###
	;  #   restore							#
	;  ### ------------------------------------------------------ ###

_print_integer_table_end :
			lw	r31,        4*4 (r29)
			lw	r16,        0*4 (r29)
			lw	r17,        1*4 (r29)
			lw	r18,        2*4 (r29)
			lw	r19,        3*4 (r29)

			addiu	r29, r29,   5*4
			jr	r31

			.end
